//===== rAthena 脚本 =======================================
//= Sader 的狩猎任务
//===== 作者: ===============================================
//= Sader1992
//https://rathena.org/board/profile/30766-sader1992/
//===== 当前版本: ===========================================
//= 5.2
//===== 兼容: ================================================ 
//= rAthena Project
//https://rathena.org/board/files/file/3579-saders-hunting-quest/
//https://github.com/sader1992/sader_scripts
//===== 说明: ===============================================
//============================================================
//============================================================
-	pointshop	Hunting_Quest_Shop	-1,#HUNT_Q_POINTS,512:1; //账号级商店
//-	pointshop	Hunting_Quest_Shop	-1,HUNT_Q_POINTS,512:1; //角色级商店
prt_in,133,39,4	script	世界之树-online#17	667,{
	function Ac;
	mes "你的任务进度：";
	for(.@i=0;.@i<getarraysize(.CataName$);.@i++){
		if(getd(Ac() + "COM_" + .@i)){
			mes .CataName$[.@i] + ": 已完成";
			setd Ac() + "POINTS",getd(Ac() + "POINTS") + .CataRP[.@i];
			if(.ExpServerRate){
				getexp .CataBXP[.@i],.CataJXP[.@i];
			}else{
				getexp2 .CataBXP[.@i],.CataJXP[.@i];
			}
			if(getd(".IRD_" + .@i)){
				for(.@j=0;.@j<getarraysize(getd(".IRD_" + .@i));.@j++){
					getitem(getd(".IRD_" + .@i + "[" + .@j + "]"),getd(".CRD_" + .@i + "[" + .@j + "]"));
				}
			}
			setd(Ac() + "CD_" + .@i,gettimetick(2) + (.CataCD[.@i] * 60 * 60));
			setd(Ac() + "COM_" + .@i,false);
			setd(Ac() + "ID_" + .@i,0);
			setd(Ac() + "C_" + .@i,0);
			setd(Ac() + "CC_" + .@i,0);
			continue;
		}
		.@CD = getd(Ac() + "CD_" + .@i);
		if(.@CD > gettimetick(2)){
			mes .CataName$[.@i] + ": " + gettimestr("%Y-%m/%d %H:%M:%S",21,.@CD);
			continue;
		}
		if(getd(Ac() + "ID_" + .@i)){
			mes .CataName$[.@i] + "(" + getd(Ac() + "CC_" + .@i) + "/" + getd(Ac() + "C_" + .@i) + ") " + strmobinfo(1,getd(Ac() + "ID_" + .@i));
			continue;
		}
		mes .CataName$[.@i] + ": 无";
	}
	.@s1 = select("打开商店:" + implode(.CataName$,":")) -2;
	clear;
	if(.@s1 == -1){
		callshop "Hunting_Quest_Shop",1;
		end;
	}
	.@CD = getd(Ac() + "CD_" + .@s1);
	if(.@CD > gettimetick(2)){
		mes "你处于冷却时间中";
		mes "请在 " + gettimestr("%Y-%m/%d %H:%M:%S",21,.@CD) + " 后再试";
		end;
	}
	if(getd(Ac() + "ID_" + .@s1)){
		mes "你的任务进度：";
		mes "(" + getd(Ac() + "CC_" + .@s1) + "/" + getd(Ac() + "C_" + .@s1) + ") " + strmobinfo(1,getd(Ac() + "ID_" + .@s1));
		if(.AllowDeleteQuest){
			mes "是否删除该任务？";
			if(select("是:否") == 2)
				end;
			mes "删除任务将无法获得任何奖励";
			if(.ApplyCoolDownOnDelete) mes "并且会进入冷却时间！";
			mes "确定吗？";
			if(select("是:否") == 2)
				end;
			clear;
			if(.ApplyCoolDownOnDelete){
				setd(Ac() + "CD_" + .@s1,gettimetick(2) + (.CataCD[.@s1] * 60 * 60));
			}
			setd(Ac() + "ID_" + .@s1,0);
			setd(Ac() + "C_" + .@s1,0);
			setd(Ac() + "CC_" + .@s1,0);
			mes "你的任务已被删除！";
			end;
		}
		end;
	}
	if(BaseLevel < .CataMN_L[.@s1] || BaseLevel > .CataMX_L[.@s1]){
		mes "你的等级需在 " + .CataMN_L[.@s1] + " 到 " + .CataMX_L[.@s1] + " 之间才能领取此任务！";
		end;
	}
	mes "是否领取任务？";
	if(select("是:否") == 2)
		end;
	copyarray(.@mid,getd(".m_id_" + .@s1),getarraysize(getd(".m_id_" + .@s1)));
	.@r = rand(getarraysize(.@mid));
	copyarray(.@mmn,getd(".m_mn_" + .@s1),getarraysize(getd(".m_mn_" + .@s1)));
	copyarray(.@mmx,getd(".m_mx_" + .@s1),getarraysize(getd(".m_mx_" + .@s1)));
	setd(Ac() + "ID_" + .@s1,.@mid[.@r]);
	setd(Ac() + "C_" + .@s1,rand(getd(".m_mn_" + .@s1 + "[" + .@r + "]"),getd(".m_mx_" + .@s1 + "[" + .@r + "]")));
	setd(Ac() + "CC_" + .@s1,0);
	mes "请击杀：";
	mes "(" + getd(Ac() + "CC_" + .@s1) + "/" + getd(Ac() + "C_" + .@s1) + ") " + strmobinfo(1,getd(Ac() + "ID_" + .@s1));
end;

OnNPCKillEvent:
	for(.@i=0;.@i<getarraysize(.CataName$);.@i++){
		if(getd(Ac() + "ID_" + .@i) == killedrid && !getd(Ac() + "COM_" + .@i)){
			setd(Ac() + "CC_" + .@i,getd(Ac() + "CC_" + .@i) + 1);
			dispbottom "狩猎任务 [" + .CataName$[.@i] + "]: (" + getd(Ac() + "CC_" + .@i) + "/" + getd(Ac() + "C_" + .@i) + ") " + strmobinfo( 1,getd(Ac() + "ID_" + .@i));
			if(getd(Ac() + "CC_" + .@i) >= getd(Ac() + "C_" + .@i)){
				dispbottom "你的 " + .CataName$[.@i] + " 狩猎任务已完成。";
				setd(Ac() + "COM_" + .@i,true);
			}
		}
	}
end;

OnHuntingCheck:
	dispbottom "你的狩猎任务进度：";
	for(.@i=0;.@i<getarraysize(.CataName$);.@i++){
		if(getd(Ac() + "COM_" + .@i)){
			dispbottom .CataName$[.@i] + ": 已完成";
			continue;
		}
		.@CD = getd(Ac() + "CD_" + .@i);
		if(.@CD > gettimetick(2)){
			dispbottom .CataName$[.@i] + ": " + gettimestr("%Y-%m/%d %H:%M:%S",21,.@CD);
			continue;
		}
		if(getd(Ac() + "ID_" + .@i) == killedrid && !getd(Ac() + "COM_" + .@i)){
			dispbottom .CataName$[.@i] + "(" + getd(Ac() + "CC_" + .@i) + "/" + getd(Ac() + "C_" + .@i) + ") " + strmobinfo(1,getd(Ac() + "ID_" + .@i));
			continue;
		}
		dispbottom .CataName$[.@i] + ": 无";
	}
end;

	function	Ac	{
		if(.AccountBase)
			return "#" + .variable_name$;
		return .variable_name$;
	}

	function	AddCata	{
		.@s = getarraysize(.CataName$);
		set getarg(1),.@s;
		.CataName$[.@s] = getarg(0);
		.CataMN_L[.@s] = getarg(2);
		.CataMX_L[.@s] = getarg(3);
		.CataCD[.@s] = getarg(4);
		.CataBXP[.@s] = getarg(5);
		.CataJXP[.@s] = getarg(6);
		.CataRP[.@s] = getarg(7);
		for(.@i=8;.@i<getargcount();.@i+=2){
			.@s2 = getarraysize(getd(".IRD_" + .@s));
			setd ".IRD_" + .@s + "[" + .@s2 + "]",getarg(.@i);
			setd ".CRD_" + .@s + "[" + .@s2 + "]",getarg(.@i+1);
		}
		return;
	}

	function	AddMob	{
		.@s = getarraysize(getd(".m_id_" + getarg(0)));
		setd ".m_id_" + getarg(0) + "[" + .@s + "]",getarg(1);
		setd ".m_mn_" + getarg(0) + "[" + .@s + "]",getarg(2);
		setd ".m_mx_" + getarg(0) + "[" + .@s + "]",getarg(3);
		return;
	}

OnInit:
	.AccountBase = true;//任务按账号记录？
	.ExpServerRate = false;//经验是否按服务器倍率？
	.AllowDeleteQuest = true;//允许玩家删除任务且不获得奖励
	.ApplyCoolDownOnDelete = true;//删除任务是否应用冷却时间
	.variable_name$ = "HUNT_Q_";//若修改此变量名需同步修改商店积分名
	bindatcmd("MyQuest",strnpcinfo(3)+"::OnHuntingCheck",0,99);
	freeloop(1);
	//可添加任意数量的分类，建议不超过25
	//AddCata(<"名称">,<.var>,<最低等级>,<最高等级>,<冷却小时>,<基础经验>,<职业经验>,<积分奖励>,<物品ID>,<数量>{,<物品ID>,<数量>{,<物品ID>,<数量>}})
	//AddCata("简单",.Easy,1,50,4,5000,250,50);
	//AddCata("普通",.Normal,25,75,5,10000,1000,100);
	//AddCata("困难",.Hard,50,80,6,25000,5000,150);
	AddCata("接取任务",.Indeterminate,75,99,7,50000,10000,250);
	
	
	//可添加任意数量的怪物
	//AddMob(<.var>,<怪物ID>,<最少数量>,<最多数量>);
	//AddMob(.Easy,1002,100,100);
	//AddMob(.Easy,1275,50,75);
	//AddMob(.Easy,1737,50,75);
	
	//AddMob(.Normal,1002,150,150);
	//AddMob(.Normal,1275,75,100);
	//AddMob(.Normal,1737,75,100);
	
	//AddMob(.Hard,1002,10,15);
	//AddMob(.Hard,1275,10,15);
	//AddMob(.Hard,1737,10,15);
	
	AddMob(.Indeterminate,1002,250,250);
	AddMob(.Indeterminate,1275,125,150);
	AddMob(.Indeterminate,1737,125,150);
	AddMob(.Indeterminate,1004,100,150);
	AddMob(.Indeterminate,1011,100,150);
	AddMob(.Indeterminate,1013,100,150);
	AddMob(.Indeterminate,1057,75,120);
	AddMob(.Indeterminate,1113,75,120);
	AddMob(.Indeterminate,1365,50,80);
	AddMob(.Indeterminate,1957,20,30);

 	settitleicon(getnpcid(0),0,"阿飞");
 	while(1){
 		showscript "[ 每日狩猎 ]";
		sleep 1000;
 	}	
 	end;
end;
}
